ulam 1;
use EventWindow;
use Empty;

quark Telomere {
  typedef Unsigned(3) Tail;
  typedef Unsigned(6) SiteNum;
  EventWindow ew;
  Tail gen;

  /**
    Maybe duplicate ew[from] to ew[to].  Duplicates only if ew[from]
    has a telomere that hasn't run out, and ew[to] is a live, empty
    site.

    \return false only if it failed to dup because of an unacceptable
    destination, true otherwise.
   */
  Bool dup(SiteNum from, SiteNum to) {
    Atom a = ew[from];
    if (a as Telomere) {
      if (a.gen > 0) {
        if (!ew.isLive(to) || !(ew[to] is Empty))
          return false;
        a.gen -= 1;
        ew[to] = a;
        ew[from] = a;
      }
    }
    return true;
  }
  Void setGen(Tail generation) {
    gen = generation;
  }
}
