/**
   Four lines.
   \symbol Bx
   \color #f0a

   \author Dave Ackley
   \license public-domain
*/
element Box
{
  EventWindow ew;
  typedef EventWindow.Symmetry Sym;

  QLine(4) m_line;
  Sym m_sym;

  Void nextSym() {
    if (m_sym == ew.cSYMMETRY_270L) m_sym = ew.cSYMMETRY_000L;
    else ++m_sym;
  }
  Void prevSym() {
    if (m_sym == ew.cSYMMETRY_000L) m_sym = ew.cSYMMETRY_270L;
    else --m_sym;
  }
  Void behave() {
    ew.changeSymmetry(m_sym);

    m_line.update();
    if (m_line.isMax() && ew[4] is Empty) {
      Box b = self;
      b.m_line.m_position = m_line.m_position.minof;
      b.nextSym();
      ew[4] = b;
    }

    if (m_line.isMin() && ew[3] is Empty) {
      Box b = self;
      b.m_line.m_position = m_line.m_position.maxof;
      b.prevSym();
      ew[3] = b;
    }
  }
}
